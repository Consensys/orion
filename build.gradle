plugins {
  id "com.diffplug.gradle.spotless" version "3.6.0"
  id 'com.palantir.git-version' version '0.10.0'
}

apply plugin: 'java-library'
apply plugin: "jacoco"

repositories {
  jcenter()
}

version = '0.1.0-' + versionDetails().gitHash[0..4] + '-' + getTimestamp()

dependencies {
  compile 'io.netty:netty-codec-http:4.1.17.Final'
  compile 'io.netty:netty-handler:4.1.17.Final'
  compile 'io.netty:netty-transport-native-epoll:4.1.17.Final:linux-x86_64'
  compile 'io.netty:netty-transport-native-kqueue:4.1.17.Final:osx-x86_64'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.58'
  compile 'com.moandjiezana.toml:toml4j:0.7.2'
  compile 'com.jcabi:jcabi-manifests:1.1'

  implementation 'org.mapdb:mapdb:3.0.5'
  implementation group: 'com.muquit.libsodiumjna', name: 'libsodium-jna', version: '1.0.4'
  testImplementation 'junit:junit:4.12'

  // vertx
  compile 'io.vertx:vertx-core:3.5.0'
  compile 'io.vertx:vertx-web:3.5.0'

  // http client
  implementation 'com.squareup.okhttp3:okhttp:3.9.1'

  // mock http server (unit test purposes)
  testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'

  // serialization
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.0'

  // logger
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
}


jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'net.consensys.athena.api.cmd.Athena'
        )
    }
}

allprojects {
    versionTxt()
}

task run(type: JavaExec) {
  description = 'Run Athena. Use -Pargs="" to pass aruments'
  classpath = sourceSets.main.runtimeClasspath
  standardInput = System.in

  main = 'net.consensys.athena.api.cmd.Athena'
  if (project.hasProperty("args")) {
    args project.getProperty('args').split()
  }
}
spotless {
  java {
    removeUnusedImports()
    googleJavaFormat()
    importOrder 'net.consensys', 'java', ''
  }
}

def getTimestamp() {
    def date = new Date()
    return date.format('yyyy-MM-dd-HH-mm-ss-SSS')
}

def versionTxt()  {
    new File("$projectDir/src/main/resources/version.txt").text = """Application-name: $project.name
Version: $version
"""
}
